include("SudokuNode.jl")
include("SudokuEdge.jl")

using Lazy: @>, @>>, @as
using Base.Iterators: product, flatten

mutable struct SudokuGraph
    nodes::Vector{SudokuNode}
    edges::Vector{Edge}

    size::Int

    function SudokuGraph(size::Int)
        #=
        The node coordinates are (row, column, cell),
        generated by creating the cartesian set of (row, col)
        and then assigning the cell based on a formula.
        =#

        nodes = @as x begin
            1:size^2
            product(x, x)
            SudokuNode.(x)
            set_cell!.(x, size)
            reshape(x, size^4)
        end

        edges = nodes_to_edges(nodes)

        return new(nodes, edges, size)
    end
end

function get_node(coordinates::Tuple{Int,Int}, graph::SudokuGraph)::SudokuNode
    return @>> begin
        graph.nodes
        filter(node -> node.coordinates == coordinates)
        pop!
    end
end

function get_cell(graph::SudokuGraph, i::Int)::Vector{SudokuNode}
    return @>> begin
        graph.nodes
        filter(node -> node.cell == i)
        collect
    end
end

function Base.print(g::SudokuGraph)
    format_blank = x -> x == 0 ? " " : string(x)

    println()
    width = g.size^2
    for i in 1:width
        for j in 1:width
            @> begin
                get_node((i, j), g)
                get_value
                format_blank
                string(j % g.size == 0 && j != width ? " |" : "")
                string(j == width ? "\n" : " ")
                print
            end
        end
        if i % g.size == 0 && i != width
            println(repeat('-', width * 2 + g.size + (g.size % 2 == 0)))
        end
    end
    println()
end

function get_neighbors(node::SudokuNode, graph::SudokuGraph)::Vector{SudokuNode}
    return @>> graph.edges begin
        filter(edge -> node in edge)
        map(get_nodes)
        flatten
        collect
        filter(x -> !isequal(node, x))
    end
end

function get_saturated_values(node::SudokuNode, graph::SudokuGraph)::Vector{Int}
    return @>> begin
        get_neighbors(node, graph)
        filter(x -> x.value > 0)
        get_value.()
        unique
    end
end

function get_saturation(node::SudokuNode, graph::SudokuGraph)::Int
    return get_saturated_values(node, graph) |> length
end

function get_possible_values(node::SudokuNode, graph::SudokuGraph)::Vector{Int}
    sv = get_saturated_values(node, graph)
    return collect(filter(x -> x âˆ‰ sv, 1:graph.size^2))
end

function set_possible_values!(node::SudokuNode, graph::SudokuGraph)::SudokuNode
    node.possible_values = get_possible_values(node, graph)
    return node
end

function unset_value!(node::SudokuNode, graph::SudokuGraph)::SudokuNode
    node.value = 0
    set_possible_values!(node, graph)
    return node
end

function get_blank_nodes(graph::SudokuGraph)::Vector{SudokuNode}
    return @>> begin
        graph.nodes
        filter(x -> get_value(x) == 0)
        collect
    end
end

function get_nonblank_nodes(graph::SudokuGraph)::Vector{SudokuNode}
    return @>> begin
        graph.nodes
        filter(x -> get_value(x) != 0)
        collect
    end
end
